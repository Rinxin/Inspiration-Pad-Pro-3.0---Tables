use: nbos\names\orc.ipt

Table: OrcHuntingParty
[@OrcHuntingPartyLeader]\n<ul><li>[@{2d4+2} RandomOrc >> implode </li><li>]</li>[@Wargs]</ul>


Table: OrcHuntingPartyLeader
1:<b>[@MasterOrcName]</b>\nHit Points: {2d8+5}\nArmor: [#{1d10+10} OrcArmor]\nBase Attack: +3 to hit, +2 damage\nWeapons: [#{1d10+10} OrcWeapons]\nSpecial Skills: [@OrcSkills]\nLanguages: [@OrcLanguages]\nWealth: [#{1d20+10} OrcWealth]


Table: RandomOrc
1:[@MasterOrcName]\nHit Points: {1d8+2}\nArmor: [#{1d15} OrcArmor]\nBase Attack: +1 to hit, +1 damage\nWeapons: [#{1d20} OrcWeapons]\nSpecial Skills: [@OrcSkills]\nLanguages: [@OrcLanguages]\nWealth: [#{1d20} OrcWealth]


Table: OrcWeapons
1-3:none (1d3)
4-8:club (1d6)
9-12:spear (1d8)
13-17:long sword (1d8)
18-22:falchion (2d4)
23-40:great sword (1d10) 

Table: OrcArmor
1-2:none (AC 10)
3:shield (AC 11)
4-6:hide (AC 13)
7-8:hide & shield (AC 14)
9-10:leather (AC 12)
11:leather & shield (AC 13)
12-14:studded leather (AC 13)
15:studded leather & shield (AC 14)
16-18:chain shirt (AC 14)
19-20:chain shirt & shield (AC 14)
21-23:full chain (AC 15)
24:full chain & shield (AC 16)
25-100:plate maile (AC 18)

Table: OrcWealth
Set: GemCount={1d3+1}
1-5: none
6-10: {3d6} cp
11-15: {3d6} sp, {3d8} cp
16-20: {3d6} gp, {3d8} sp, {3d10} cp
21-25: {3d6} pp, {3d10} sp, {3d20} cp
25-30: {$GemCount} small gems ([@{$GemCount} OrcGems >> implode]), {3d6*5} pp, {3d10*5} sp, {3d20*5} cp

Table: OrcGems
{1d100*5} gp


Table: OrcSkills
10:none
Sneak Attack, +2d6
Escape Artist Skill {1d4+4}
Unusual Spot Skill {1d4+4}
Move Silently {1d4+4}
Jump {1d4+4}
Tracking {1d4+4}

Table: OrcLanguages
Shuffle: Orc2ndLanguage
12: Orcish 
6:Orcish, plus [@Orc2ndLanguage]
3:Orcish, plus [!2 Orc2ndLanguage >> implode]
1:Orcish, plus [!3 Orc2ndLanguage >> implode]

Table: Orc2ndLanguage
5:[|passable|passable|passable|passable|good|good|exceptional] Common
2:[|passable|passable|passable|passable|good|good|exceptional] Dwarvish
[|passable|passable|passable|passable|good|good|exceptional] Elvish
2:[|passable|passable|passable|passable|good|good|exceptional] Ogre
2:[|passable|passable|passable|passable|good|good|exceptional] Hill Giant
2:[|passable|passable|passable|passable|good|good|exceptional] Goblin



Table: Wargs
Set: nWargs={1d2+1}
6:\z
1:<li>1 Dire Wolf\nhits: [@{$nWargs} WargHits >> implode]\nArmor: 14\nAttack:Bite +11 for 1d8+10</li>
1:<li>{$nWargs} Dire Wolves\nhits: [@{$nWargs} WargHits >> implode]\nArmor: 14\nAttack: Bite +11 for 1d8+10</li>

Table: WargHits
{6d8+18}
